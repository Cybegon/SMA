# detect the OS
if(${CMAKE_SYSTEM_NAME} STREQUAL "Windows")
    set(CYBEGON_OS_WINDOWS 1)
    add_definitions(-DCYBEGON_WINDOWS)

    # detect the architecture (note: this test won't work for cross-compilation)
    include(CheckTypeSize)
    check_type_size(void* SIZEOF_VOID_PTR)
    if(${SIZEOF_VOID_PTR} STREQUAL "4")
        set(ARCH_32BITS 1)
    elseif(${SIZEOF_VOID_PTR} STREQUAL "8")
        set(ARCH_64BITS 1)
    else()
        message(FATAL_ERROR "Unsupported architecture")
        return()
    endif()
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Linux")
    set(CYBEGON_OS_UNIX 1)
    add_definitions(-DCYBEGON_UNIX)

    if(ANDROID)
        set(CYBEGON_OS_ANDROID 1)
        add_definitions(-DCYBEGON_ANDROID)
    else()
        set(CYBEGON_OS_LINUX 1)
        add_definitions(-DCYBEGON_LINUX)
    endif()
elseif(CMAKE_SYSTEM_NAME MATCHES "^k?FreeBSD$")
    set(CYBEGON_OS_FREEBSD 1)
    add_definitions(-DCYBEGON_FREEBSD)
elseif(CMAKE_SYSTEM_NAME MATCHES "^OpenBSD$")
    set(CYBEGON_OS_OPENBSD 1)
    add_definitions(-DCYBEGON_OPENBSD)
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Darwin")
    if(IOS)
        set(CYBEGON_OS_IOS 1)
        add_definitions(-DCYBEGON_IOS)
    else()
        set(CYBEGON_OS_MACOSX 1)
        add_definitions(-DCYBEGON_MACOSX)

        # detect OS X version. (use '/usr/bin/sw_vers -productVersion' to extract V from '10.V.x'.)
        EXEC_PROGRAM(/usr/bin/sw_vers ARGS -productVersion OUTPUT_VARIABLE MACOSX_VERSION_RAW)
        STRING(REGEX REPLACE "10\\.([0-9]+).*" "\\1" MACOSX_VERSION "${MACOSX_VERSION_RAW}")
        if(${MACOSX_VERSION} LESS 7)
            message(FATAL_ERROR "Unsupported version of OS X: ${MACOSX_VERSION_RAW}")
            return()
        endif()
    endif()
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Android")
    set(CYBEGON_OS_ANDROID 1)
    add_definitions(-DCYBEGON_ANDROID)

# comparing CMAKE_SYSTEM_NAME with "CYGWIN" generates a false warning depending on the CMake version
# let's avoid it so the actual error is more visible
elseif(${CYGWIN})
    message(FATAL_ERROR "Unfortunately CYBEGON doesn't support Cygwin's 'hybrid' status between both Windows and Linux derivatives.\nIf you insist on using the GCC, please use a standalone build of MinGW without the Cygwin environment instead.")
else()
    message(FATAL_ERROR "Unsupported operating system or environment")
    return()
endif()

# detect the compiler and its version
# Note: on some platforms (OS X), CMAKE_COMPILER_IS_GNUCC is true
# even when CLANG is used, therefore the Clang test is done first
if(CMAKE_C_COMPILER MATCHES "clang[+][+]" OR CMAKE_C_COMPILER_ID MATCHES "Clang")
   # CMAKE_C_COMPILER_ID is an internal CMake variable subject to change,
   # but there is no other way to detect CLang at the moment
   set(CYBEGON_COMPILER_CLANG 1)
   execute_process(COMMAND "${CMAKE_C_COMPILER}" "--version" OUTPUT_VARIABLE CLANG_VERSION_OUTPUT)
   string(REGEX REPLACE ".*clang version ([0-9]+\\.[0-9]+).*" "\\1" CYBEGON_CLANG_VERSION "${CLANG_VERSION_OUTPUT}")
elseif(CMAKE_COMPILER_IS_GNUCC)
    set(CYBEGON_COMPILER_GCC 1)
    add_definitions(-DCYBEGON_COMPILER_GCC)
    execute_process(COMMAND "${CMAKE_C_COMPILER}" "-dumpversion" OUTPUT_VARIABLE GCC_VERSION_OUTPUT)
    string(REGEX REPLACE "([0-9]+\\.[0-9]+).*" "\\1" CYBEGON_GCC_VERSION "${GCC_VERSION_OUTPUT}")
    execute_process(COMMAND "${CMAKE_C_COMPILER}" "--version" OUTPUT_VARIABLE GCC_COMPILER_VERSION)
    string(REGEX MATCHALL ".*(tdm[64]*-[1-9]).*" CYBEGON_COMPILER_GCC_TDM "${GCC_COMPILER_VERSION}")
    execute_process(COMMAND "${CMAKE_C_COMPILER}" "-dumpmachine" OUTPUT_VARIABLE GCC_MACHINE)
    string(STRIP "${GCC_MACHINE}" GCC_MACHINE)
    if(GCC_MACHINE MATCHES ".*w64.*")
        set(CYBEGON_COMPILER_GCC_W64 1)
    endif()
elseif(MSVC)
    set(CYBEGON_COMPILER_MSVC 1)
    add_definitions(-DCYBEGON_COMPILER_MSVC)
    if(MSVC_VERSION EQUAL 1400)
        set(CYBEGON_MSVC_VERSION 8)
    elseif(MSVC_VERSION EQUAL 1500)
        set(CYBEGON_MSVC_VERSION 9)
    elseif(MSVC_VERSION EQUAL 1600)
        set(CYBEGON_MSVC_VERSION 10)
    elseif(MSVC_VERSION EQUAL 1700)
        set(CYBEGON_MSVC_VERSION 11)
    elseif(MSVC_VERSION EQUAL 1800)
        set(CYBEGON_MSVC_VERSION 12)
    elseif(MSVC_VERSION EQUAL 1900)
        set(CYBEGON_MSVC_VERSION 14)
    endif()
else()
    message(FATAL_ERROR "Unsupported compiler")
    return()
endif()

if (ARCH_64BITS)
    add_definitions(-DARCH_64BITS)
elseif(ARCH_32BITS)
    add_definitions(-DARCH_32BITS)
endif()
